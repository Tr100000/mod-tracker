plugins {
	id 'maven-publish'
	id 'org.quiltmc.loom' version '1.4.+'
}

base {
	archivesName = project.archives_base_name
}

version = "$project.version+${minecraft_version}"
group = project.maven_group

repositories {
	maven {
		name = "TerraformersMC Maven"
		url = "https://maven.terraformersmc.com/releases"
		content {
			includeGroup "com.terraformersmc"
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

def qsl = [".core:qsl_base", ".core:networking", ".management:client_command"]

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${mappings_version}:intermediary-v2"
	modImplementation "org.quiltmc:quilt-loader:${loader_version}"

	qsl.each { item -> 
		modImplementation "org.quiltmc.qsl${item}:${qsl_version}+${minecraft_version}"
	}
}

project.configurations.all {
	exclude(group: "net.fabricmc", module: "fabric-loader")
	exclude(group: "net.fabricmc.fabric-api")
}

loom {
	mods {
		"modtracker" {
			sourceSet("main")
		}
	}
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand 'version': version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
	// withJavadocJar()
}

jar {
	from('LICENSE') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		// Add repositories to publish to here.
	}
}
